buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['ReactNativeWebView_' + name]
  }

  // The Android Gradle plugin is only required when opening the android folder stand-alone.
  // This avoids unnecessary downloads and potential conflicts when the library is included as a
  // module dependency in an application project.
  if (project == rootProject) {
    repositories {
      google()
      jcenter()
    }

    dependencies {
      classpath("com.android.tools.build:gradle:3.6.0")
      classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}")
    }
  } else {
    repositories {
      jcenter()
    }

    dependencies {
      classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}")
    }
  }
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties['ReactNativeWebView_' + name]).toInteger()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def findReactNativeAAR() {
  def ft = fileTree("${getReactNativeAndroidRoot()}/com/facebook/react/react-native") {
    include '**/*.aar'
  }
  ft.getSingleFile()
}

task extractAARForNDKBuild(type: Copy) {
  from zipTree(findReactNativeAAR())
  into "${buildDir}/_libraries/"
  include "jni/**/libjscexecutor.so"
}
preBuild.dependsOn(extractAARForNDKBuild)


android {
  compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')
  buildToolsVersion getExtOrDefault('buildToolsVersion')
  ndkVersion '19.2.5345600'
  defaultConfig {
    minSdkVersion 16
    targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
    versionCode 1
    versionName "1.0"
    ndk {
      moduleName "reactnativecommunity-webview"
      ldLibs "log"
    }
    externalNativeBuild {
      ndkBuild {
        arguments "BUILD_DIR=${buildDir}"
      }
    }
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  sourceSets {
    main {
      jni.srcDirs = ["src/main/cpp"]
    }
  }
  externalNativeBuild {
    ndkBuild {
      path file('src/main/jni/Android.mk')
    }
  }
}

def getReactNativeAndroidRoot() {
  def found = false
  def reactNativeAndroidRoot = null

  if (rootProject.ext.has('reactNativeAndroidRoot')) {
    reactNativeAndroidRoot = rootProject.ext.get('reactNativeAndroidRoot')
  } else {
    reactNativeAndroidRoot = new File(
      projectDir,
      '/../../../node_modules/react-native/android'
    )
  }

  if (reactNativeAndroidRoot.exists()) {
    logger.info(":${project.name}:reactNativeAndroidRoot ${reactNativeAndroidRoot.canonicalPath}")
    found = true
  } else {
    def parentDir = rootProject.projectDir

    1.upto(5, {
      if (found) return true
      parentDir = parentDir.parentFile

      // See github issue #218 and PR #238 to understand this
      def androidSourcesDir = new File(
        parentDir,
        'node_modules/react-native'
      )

      def androidPrebuiltBinaryDir = new File(
        parentDir,
        'node_modules/react-native/android'
      )

      if (androidPrebuiltBinaryDir.exists()) {
        logger.info(":${project.name}:reactNativeAndroidRoot ${androidPrebuiltBinaryDir.canonicalPath}")
        reactNativeAndroidRoot = androidPrebuiltBinaryDir
        found = true
      } else if (androidSourcesDir.exists()) {
        logger.info(":${project.name}:reactNativeAndroidRoot ${androidSourcesDir.canonicalPath}")
        reactNativeAndroidRoot = androidPrebuiltBinaryDir
        found = true
      }
    })
  }

  if (!found) {
    throw new GradleException(
      "${project.name}: unable to locate React Native android sources. " +
        "Ensure you have you installed React Native as a dependency in your project and try again."
    )
  }

  return reactNativeAndroidRoot.toString()
}

repositories {
  mavenCentral()
  jcenter()
  google()

  def androidSourcesName = 'React Native sources'
  maven {
    url getReactNativeAndroidRoot()
    name androidSourcesName
  }
}

def kotlin_version = getExtOrDefault('kotlinVersion')

dependencies {
  //noinspection GradleDynamicVersion
  implementation 'com.facebook.react:react-native:+'
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

